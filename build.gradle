plugins {
    id 'org.springframework.boot' version '2.4.4' apply false
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'java'
    id 'idea'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

description = """java handbook sample code"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

// https://github.com/spring-io/platform/blob/master/platform-bom/pom.xml
// https://github.com/spring-projects/spring-boot/blob/master/spring-boot-dependencies/pom.xml

allprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << '-parameters'
    }
    compileJava {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << '-parameters'
        options.encoding = 'UTF-8'
    }

    ext['okhttp3.version'] = '4.6.0'

    dependencyManagement {
        imports {
            mavenBom(org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES)
        }

        dependencies {
            dependency 'org.mybatis.spring.boot:mybatis-spring-boot-starter:2.1.3'
            dependency 'org.mybatis:mybatis-typehandlers-jsr310:1.0.2'
            dependency 'org.webjars.npm:bootstrap:4.5.3'
            dependency "com.squareup.okhttp3:mockwebserver:" + ext['okhttp3.version']
            dependency 'com.github.jbellis:jamm:0.3.1'
            dependency 'com.google.guava:guava:30.1.1-jre'
        }
    }
}

subprojects {
    test {
        useJUnitPlatform()
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    dependencies {
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'

        testCompileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
    }
}

configurations {
    compile.exclude module: "commons-logging"
    compile.exclude group: "junit"
    compile.exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
}
